#+BEGIN_SRC sas :dir /ssh:wrds:/home/sec/eddyhu/ 
    
%let yyyy=2014;
    
data mastm_&yyyy. / view=mastm_&yyyy.;
    set taqmsec.mastm_2014:;
    SYM_ROOT=symbol_root;
    SYM_SUFFIX=symbol_suffix;
run;

proc sql;
    create table mastm_crsp_&yyyy. as
        select a.date, sym_root, sym_suffix, symbol_15, a.cusip,
        b.permno, b.permco, shrcd, exchcd,
        c.prc, c.ret, c.retx, c.shrout, c.vol, d.divamt, d.distcd
        from mastm_&yyyy. a
        left join
        crspm.dsenames b
        on substr(a.cusip,1,8) = substr(coalesce(b.ncusip, b.cusip),1,8)
        and a.date between b.namedt and coalesce(b.nameendt, today())
        left join
        crspm.dsf c
        on b.permno = c.permno
        and a.date = c.date
        left join
        crspm.dsedist d
        on c.permno = d.permno
        and c.date = d.paydt
        order by a.date, sym_root, sym_suffix;
run;

proc sort data=taqmsec.wrds_iid_&yyyy.
    out=wrds_iid_&yyyy.;
    by date sym_root sym_suffix;
run;

proc datasets nolist;
    contents data=crspm.dsf;
run;quit;

proc datasets nolist;
    contents data=mastm_&yyyy.;
run;quit;

proc datasets nolist;
    contents data=wrds_iid_&yyyy.;
run;quit;

data taqdf_&yyyy.;
    merge wrds_iid_&yyyy.(keep=date sym_root sym_suffix
        buynumtrades_lr sellnumtrades_lr oprc cprc ret_mkt_m
        vw_price_m mid_after_open
        total_vol_m total_vol_b total_vol_a)
        mastm_crsp_&yyyy.;
    by date sym_root sym_suffix;
    CCPrc = abs(coalesce(prc,cprc));
    label CCPrc='Closing Price (CRSP or TAQ)';
run;

proc sort data=taqdf_&yyyy. out=taqdf_&yyyy.x nodupkey;
    by permno date;
    where permno > .Z
        and shrcd in (10,11)
        and exchcd in (1,2,3,4);
run;

proc printto log='/dev/null';run;
proc expand data=taqdf_&yyyy.x
    out=taqdf_&yyyy.x1
    method=none;
    by permno;
    convert ccprc = CCPrcL1 / transformout = (lag 1);
    convert mid_after_open = omF1 / transformout = (lead 1);
run;
data taqdf_&yyyy.x1;
    set taqdf_&yyyy.x1;
    y_e = (buynumtrades_lr-sellnumtrades_lr)/(buynumtrades_lr+sellnumtrades_lr);
    r_d = (vw_price_m-mid_after_open+coalesce(divamt,0))/mid_after_open;
    r_o = (omF1-vw_price_m)/mid_after_open;
    rename buynumtrades_lr=n_buys sellnumtrades_lr=n_sells;
run;
proc printto;run;
%put expand &syslast. done;

proc print data=taqdf_&yyyy.x1(obs=25) width=min;
var date symbol_15 cusip permno n: r_: y_e total_vol_m;run;

#+END_SRC

#+BEGIN_SRC python
import pandas as pd
import ipyparallel as ipp
import os
os.chdir('/home/sec/eddyhu/git/pin-code')
import eo_model as eo
import gpin_model as gpin
import owr_model as owr

rc = ipp.Client()
len(rc)

df = pd.read_sas('/scratch/sec/hue/taqdf_2014x1.sas7bdat')
df['date'] = pd.to_timedelta(df.DATE, unit='D') + pd.Timestamp('1960-1-1')
df['permno'] = df.PERMNO.astype('int')
df['ticker'] = df.SYMBOL_15.str.decode('UTF-8')
df.set_index('permno date'.split(),inplace=True)
df['ticker n_buys n_sells r_d r_o y_e'.split()].to_hdf('/scratch/sec/hue/taqdf_2014.h5','data',format='table')

d = pd.read_hdf('/scratch/sec/hue/taqdf_2014.h5',where='permno==11850')

eo.fit(d.n_buys,d.n_sells)
gpin.fit(d.n_buys,d.n_sells)
owr.fit(d.y_e,d.r_d,d.r_o)
#+END_SRC
